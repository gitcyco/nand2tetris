// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl
/** 
 * The Memory chip implements the complete address space of the Hack data memory,
 * including RAM, memory mapped screen, and memory mapped keyboard.
 * Outputs the value of the memory location specified by the address input.
 * If (load == 1), sets the memory location specified by the address input
 * to the value of the in input.
 * Address space rules:
 * Only the upper 16K + 8K + 1 words of the memory are used.
 * Access to address 0 to 16383 (0x0000 to 0x3FFF) results in accessing the RAM;
 * Access to address 16384 to 24575 (0x4000 to 0x5FFF) results in accessing the Screen memory map;
 * Access to address 24576 (0x6000) results in accessing the Keyboard memory map.
 */ 
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // SEL: 00/01 -> RAM, 10 -> SCR, 11 -> KBD
    DMux4Way(in=load, sel=address[13..14], a=loadRAMA, b=loadRAMB, c=loadSCR, d=loadKBD);
    Or(a=loadRAMA, b=loadRAMB, out=loadRAM);

    // Main ram:   0x0000 -> 0x3FFF / 0000000000000000 -> 0011111111111111
    RAM16K(in=in, load=loadRAM, address[13]=address[13], address[0..12]=address[0..12], out=outRAM);

    // Screen mem: 0x4000 -> 0x5FFF / 0100000000000000 -> 0101111111111111
    Screen(in=in, load=loadSCR, address=address[0..12], out=outSCR);
    
    // KBD mem:    0x6000           / 0110000000000000    0110000000000000
    Keyboard(out=outKBD);

    // SEL: 00/01 -> RAM, 10 -> SCR, 11 -> KBD
    Mux4Way16(a=outRAM, b=outRAM, c=outSCR, d=outKBD, sel=address[13..14], out=out);
}

// TYP  SEL ADDRESS          SEL ADDRESS
// RAM: 000 0000000000000 -> 001 1111111111111
// SCR: 010 0000000000000 -> 010 1111111111111
// KBD: 011 0000000000000    011 0000000000000